
\      PROGRAM DELIVERS PELLET ON VARIABLE TIME INTERVAL 30 SEC
\    (0-60 SEC RANGE) -- SET IN LIST V
\     NUMBER OF BLOCK TRIALS/SESSION = 25 -- SET IN S.S.1
\    NUMBER OF SESSIONS = 1 -- SET IN S.S.1
\      US = FOOD-PELLET
\    5 MIN HABITUATION PERIOD PRIOR TO SESSION 1

\    *** SET SESSION PARAMETERS UNDER S.S.1  ***

\*** INPUTS
\R3= PELLET RECEPTACLE
\R1= ACTIVE NOSE-POKE HOLE
\R2= INACTIVE NOSE-POKE HOLE

\*** SET OUTPUTS
^PELLET = 3
^HOUSELIGHT = 7



\  *** VARIABLES USED IN THIS PROGRAM

\    B = ARRAY FOR SESSION PARAMETERS
\    Y = VI VALUE SAMPLER
\    V = LIST OF VI VALUES
\    W = SESSION COUNTERS
\    I = INDEX FOR DATA COLLECTION IN ARRAYS


DIM A = 4        \SESSION TIMERS
DIM B = 4        \AUTOSHAPING SESSION PARAMETERS -- SET BY EXPERIMENTER BELOW
DIM L = 10       \ARRAY FOR MAGAZINE ENTRIES -- SET EQUAL TO NUMBER OF SESSIONS
DIM E = 3        \NUMBER OF MAGAZINE ENTRIES
DIM F = 2        \NUMBER OF ACTIVE NOSE POKES
DIM G = 2        \NUMBER OF INACTIVE NOSE POKES
DIM U = 2000

\*** Set DIM K THROUGH S equal to the number of trials * sessions

\*** Z PULSES
\ Z1 -START OF SESSION
\ Z2 -START OF SUBSEQUENT SESSIONS
\ Z5 -SESSION COUNTER
\ Z6 -END OF FINAL SESSION
\ Z7 -pulse for the 2.5 end of session period

\*** VARIABLE INTERVAL (VI) VALUES
LIST V = 0",5",10",15",20",25",30",35",40",45",50",55",60"

\*** SET SESSION PARAMETERS
S.S.1,

S1,

    0.1":    SET B(0)=300;            \TIME BEFORE FIRST SESSION (IN SECONDS)
        SET B(1)=25;            \NUMBER OF PELLETS PER SESSION
        SET B(2)=1;            \NUMBER OF SESSIONS
        SET B(4)=555;    \procedure code for pretraining
        SET B(3)=1--->S2        \TIME BETWEEN SESSIONS (IN MIN)

S2,
    360':--->S2

\*** TIMER FOR START OF FIRST SESSION
S.S.2,

S1,
     #START: SET A(1) = 0 ---> S2
S2,
     1":ADD A(1);SHOW 5, SEC_ST, A(1); IF A(1)=B(0) [@TRUE,@FALSE]
                    @TRUE:--->S3
                    @FALSE:--->S2
S3,
    0.1": SET I=0; SET W=0; SET P = 0;Z1 --->S4
S4,
    360':--->STOPABORTFLUSH


\*** PRETRAINING SESSION
S.S.3,
S1,
    #Z1 ! #Z2:SET P=0--->S2

S2,
    0.1":ON ^HOUSELIGHT;ADD W; SHOW 6, SESSION, W--->S3

S3,
    0.1":RANDI Y = V; ---> S4
S4,
    Y#T:ON ^PELLET;z8;ADD P; SHOW 1, TRIAL, P--->S5
S5,
    0.5":OFF ^PELLET; IF P = B(1)[@TRUE,@FALSE]
               @TRUE: Z7--->S1
          @FALSE:--->S3


\*** MAGAZINE RESPONSE COUNTER
S.S.5,
S1,
    #Z1 ! #Z2: SET E(0)=0--->S2

S2,
    #R3: ADD E(0);SHOW 2, MAG_ENT,E(0)--->S2
    #Z5:--->S1


\*** ACTIVE NOSE POKE RESPONSE COUNTER
S.S.6,
S1,
    #Z1 ! #Z2: SET F(0)=0--->S2

S2,
    #R1: ADD F(0);SHOW 3, ACTIVE NP,F(0)--->S2
    #Z5:--->S1


\*** INACTIVE NOSE POKE RESPONSE COUNTER
S.S.7,
S1,
    #Z1 ! #Z2: SET G(0)=0--->S2

S2,
    #R2: ADD G(0);SHOW 4, INACTIVE NP,G(0)--->S2
    #Z5:--->S1


\*** SESSION TIMER
S.S.8,
S1,
    #Z1 ! #Z2:SET A(2)=0--->S2

S2,
    1":ADD A(2); SHOW 5, SES_MIN, A(2)/60--->S2
    #Z5:--->S1


\*** INTER-SESSION INTERVALS
S.S.9,

S1,
    #Z5:OFF ^HOUSELIGHT;SET A(3)=0--->S2
S2,
    1":ADD A(3); SHOW 6, ISI, A(3)/60; IF A(3)=B(3) [@TRUE, @FALSE]
                @TRUE:Z2--->S1
                @FALSE:--->S2

\*** END OF SESSION
S.S.10,

S1,
    #Z5:IF W >= B(2) [@TRUE,@FALSE]
            @TRUE:SET L(I)=E(0)--->S2
            @FALSE:SET L(I)=E(0);ADD I;SET E(0)=0;Z2--->S1


S2,
    0.5": --->STOPABORTFLUSH

S.S.11, \*** adds 2.5 min or 150sec to end of session activated by #z7, after the timer, gives a z5 pulse

S1,
     #Z7: SET A(4) = 0 ---> S2
S2,
     1":ADD A(4);SHOW 7, SEC_END, A(4); IF A(4)=150 [@TRUE,@FALSE]
                    @TRUE:--->S3
                    @FALSE:--->S2
S3,
    0.1": Z5 --->S1

\*** COLLECTION FOR TIMESTAMPS

S.S.16,
S1,
  #START: ---> S2

S2,     \ Increment O every 10 ms
  0.01": ADD O ---> SX

S.S.17,
S1,
  #START:set x=0 ---> S2

S2,
  #R3: SET U(X) = O + 0.44; ADD X--->S2 \R3 is magazine entry
  #Z8: SET U(X) = O + 0.55; ADD X--->S2 \Z8 comes from pellet delivery

