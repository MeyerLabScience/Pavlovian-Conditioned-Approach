\      AUTOSHAPING.MPC PROGRAM BY HANS S. CROMBAG (ROBINSON LAB)--> S. FLAGEL 2.26.04 (NEWEST w/NP)
\      PROGRAM DELIVERS PAIRED CS (LEVER/LIGHT)-US (PELLET) ON VARIABLE TIME (VT) 90 SEC
\     SCHEDULE (30-150 SEC RANGE)
\      TOTAL PROGRAM TIME = NUMBER OF TRIAL * SESSIONS * VT
\     NUMBER OF BLOCK TRIALS = 25
\      US = BANANA-PELLET
\      CS = 8 SEC LEVER/LIGHT
\    *** SET SESSION PARAMETERS UNDER S.S.1  ***

\*** INPUTS
\R4= LEVER
\R3= PELLET RECEPTACLE

\*** SET OUTPUTS
^LEVER = 13
^CUELIGHT = 11
^PELLET = 3
^HOUSELIGHT = 7

\  *** VARIABLES USED IN THIS PROGRAM

\  Q = SWITCH FOR CS OFF (0) OR ON (1)
\  Y = VI VALUE SAMPLER
\  V = LIST OF VT VALUES
\  W = SESSION COUNTERS
\  P = PELLET/TRIAL COUNTER
\  I = INDEX FOR DATA COLLECTION IN ARRAYS K,M,O
\  J = INDEX FOR LEVER LATENCY DATA COLLECTION - ARRAY L
\  T = INDEX FOR MAGAZINE LATENCY DATA COLLECTION - ARRAY N

\ ** DO NOT CHANGE THE FOLLOWING PARAMETERS
DIM A = 6        \SESSION TIMERS
DIM B = 8         \AUTOSHAPING SESSION PARAMETERS (SET VALUE IN S.S.1)
DIM C = 1         \NUMBER OF LEVER PRESS (DURING CS)/SESSION
DIM D = 1         \NUMBER OF MAGAZINE ENTRIES DURING CS/SESSION
DIM E = 1         \NUMBER OF MAGAZINE ENTRIES DURING ISI PERIOD/SESSION

\ ** SET THE FOLLOWING PARAMETERS EQUAL TO THE # OF SESSIONS
DIM F = 2         \NUMBER OF LEVER PRESS (DURING CS)/SESSION
DIM G = 2         \NUMBER OF MAGAZINE ENTRIES DURING CS/SESSION
DIM H = 2         \NUMBER OF MAGAZINE ENTRIES POST PERIOD/SESSION


\ *** SET THE FOLLOWING PARAMETERS EQUAL TO THE NUMBER OF SESSIONS X THE NUMBER OF TRIALS
DIM K = 30         \ARRAY FOR NUMBER OF LEVER PRESS/TRIAL
DIM L = 30         \ARRAY FOR LATENCY OF LEVER PRESS/TRIAL
DIM M = 30         \ARRAY FOR NUMBER OF MAGAZINE ENTRIES DURING CS PERIOD/TRIAL
DIM N = 30         \ARRAY LATENCY MAGAZINE ENTRY DURING CS PERIOD/TRIAL
DIM R = 30         \FREQUENCY OF MAGAZINE ENTRIES DURING ISI (H/VI)
DIM S = 30         \ARRAY VT VALUES USED
DIM U = 2000


\*** Z PULSES USED
\ Z1 -START OF SESSION
\ Z2 -START OF SUBSEQUENT SESSIONS
\ Z3 -ONSET CS
\ Z5 -END OF SESSION
\ Z6 -LATENCY TRIGGER
\ Z7 -INTER-SESSION TRIGGER

\*** VARIABLE TIME (VT) VALUES
LIST V = 30",35",40",45",50",55",60",65",70",75",80",85",90",95",100",105",110",115",120",125",130",135",140",145",150"



\*** SET SESSION PARAMETERS
S.S.1,

S1,

    0.1":    SET B(0)=60;            \TIME BEFORE FIRST SESSION (IN SECONDS)
        SET B(1)=25;            \NUMBER OF CS-US TRIALS PER SESSION
        SET B(2)=8;            \CS DURATION (IN SECONDS)
        SET B(3)=1;            \NUMBER OF SESSIONS
        SET B(4)=1;    \TIME BETWEEN SESSIONS/INTER-SESSION INTERVAL (IN SEC)
      set b(8)=666--->S2        \PROCEDURE CODE FOR ROW PROFILE

S2,
    1800':--->S2

\*** TIMER FOR START OF FIRST SESSION
S.S.2,

S1,
     #START: SET A(1) = 0--->S2
S2,
     1":ADD A(1);SHOW 5, SEC_ST, A(1); IF A(1)=B(0) [@TRUE,@FALSE]
                    @TRUE:--->S3
                    @FALSE:--->S2
S3,
    0.1": SET I=0; SET J=0; SET W = 0;Z1 --->S1



\*** AUTOSHAPING SESSION
S.S.3,
S1,
    #Z1 ! #Z2:SET C(1)=0;SET D(1)=0;SET E(1)=0--->S2

S2,
    0.1":SET P=0;ON ^HOUSELIGHT--->S3

S3,
    0.1":RANDI Y = V; SHOW 8,VI,Y/100---> S4
S4,
    Y#T:Z6;ON ^LEVER, ^CUELIGHT;SET Q =1;ADD P; SHOW 1, TRIAL, P--->S5
S5,
    1":ADD A(2); IF A(2) >= B(2)[@TRUE,@FALSE]
       @TRUE: OFF ^LEVER, ^CUELIGHT; SET Q = 0; ON ^PELLET;SET A(2)=0--->S6
    @FALSE:--->S5
S6,
    0.1":OFF ^PELLET; IF P >= B(1) [@TRUE, @FALSE]
        @TRUE:--->S7
        @FALSE:--->S3
S7,
        20":Z5--->S1


\*** CS LEVER PRESS COUNTER
S.S.4,
S1,
    #Z1 ! #Z2:SET C(0)=0--->S2

S2,
     #R4:IF Q = 1 [@TRUE,@FALSE]
             @TRUE: ADD C(0), C(1)--->S2
             @FALSE: --->S2
        #Z5--->S1


\*** MAGAZINE RESPONSE COUNTER
S.S.5,
S1,
    #Z1 ! #Z2:SET E(1)=0; SET D(1)=0--->S2

S2,
    #R3:IF Q = 1 [@TRUE,@FALSE]
             @TRUE: ADD D(0), D(1)--->S2
             @FALSE: ADD E(0), E(1)--->S2
    #Z5--->S1


\*** SCREEN DISPLAY
S.S.8,
S1,
     #Z1 ! #Z2--->S2
S2,
     0.1": SHOW 2, CS_MAG, D(1); SHOW 3,NCS-MAG,E(1); SHOW 4, CS_LEVER, C(1)--->S2


\*** SESSION TIMER
S.S.9,
S1,
    #Z1 ! #Z2:SET A(6)=0;ADD W; SHOW 12, SESSION, W--->S2

S2,
    1": ADD A(6); SHOW 5, SES_MIN, A(6)/60--->S2
    #Z5: OFF ^HOUSELIGHT, ^PELLET, ^LEVER;SET A(6)=0--->S1


\*** INTER-SESSION INTERVALS
S.S.10,

S1,
    #Z7:SET A(3)=0--->S2
S2,
    1":ADD A(3); SHOW 5, ISI, A(3); IF A(3)=B(4) [@TRUE, @FALSE]
                @TRUE:Z2--->S1
                @FALSE:--->S2

\*** END OF SESSION
S.S.11,

S1,
    #Z5:IF W = B(3) [@TRUE, @FALSE]
    @TRUE:Z8--->S2
    @FALSE:Z7--->S1

S2,
       1":--->STOPABORTFLUSH

\*** COLLECTION FOR TRIAL LEVER PRESSES (K), MAGAZINE ENTRIES (M), LEVER PRESS LATENCY (L) AND MAGAZINE ENTRY LATENCY (N

S.S.12,
S1,
    #Z6 ! #Z8:IF P>=2 [@TRUE,@FALSE]
        @TRUE:SET K(I)=C(0);SET C(0)=0;SET M(I)=D(0);SET D(0)=0;SET R(I)=E(0)/(Y/100);SET E(0)=0;SET S(I)=Y/100; SET L(I)=A(4)/10; SET A(4)=0; SET N(I)=A(5)/10;SET A(5)=0;ADD I--->S1
       @FALSE:--->S1


\*** COLLECTION FOR SESSION LEVER PRESSES (K),MAGAZINE ENTRIES (M),LEVER PRESS LATENCY (L) AND MAGAZINE ENTRY LATENCY (N)

S.S.13,

S1,
    #Z5:SET F(W)=C(1); SET G(W)=D(1); SET H(W)=E(1)--->S1


\*** LATENCY 1ST LEVER PRESS
S.S.14,
S1,
    #Z6:--->S2
S2,
    0.1":ADD A(4) --->S2
    #R4:SHOW 6, LAT_LP,A(4)/10 --->S1
    #Z5--->S1

\*** LATENCY MAGAZINE ENTRY POST CS ONSET
S.S.15,
S1,
    #Z6:--->S2
S2,
        0.1":ADD A(5) --->S2
    #R3:SHOW 7, LAT_MAG,A(5)/10--->S1
        #Z5--->S1

\*** COLLECTION FOR TIMESTAMPS

S.S.16,
S1,
  #START: ---> S2

S2,     \ Increment O every 10 ms
  0.01": ADD O ---> SX

S.S.17,
S1,
  #START: set x=0---> S2

S2,
  #R4: SET U(X) = O + 0.11; ADD X--->S2 \R4 is lever press
  #R3: SET U(X) = O + 0.22; ADD X--->S2 \R3 is magazine entry
  #Z6: SET U(X) = O + 0.33; ADD X--->S2 \Z6 comes from CS onset
  

\************************************************************************
\* DATA FILE KEY                                            *
\*                                                        *
\* F -NUMBER OF LEVER PRESSES PER SESSION                            *
\* G -NUMBER OF CS PERIOD MAGAZINE ENTRIES PER SESSION                *
\* H -NUMBER OF NON-CS PERIOD MAGAZINE ENTRIES PER SESSION            *
\* K -NUMBER OF LEVER PRESSES PER TRIAL                            *
\* L -LATENCY OF LEVER PRESS PER TRIAL                            *
\* M -NUMBER OF CS PERIOD MAGAZINE ENTRIES PER TRIAL                *
\* N -LATENCY MAGAZINE ENTRY PER TRIAL
\* O -NUMBER OF NOSE POKES IN THE ACTIVE HOLE PER TRIAL                            *
\* R -FREQUENCY OF NON-CS PERIOD MAGAZINE ENTRIES PER TRIAL            *
\* S -VARIABLE TIME VALUES USED DURING RUN
\* U -NUMBER OF NOSE POKES IN THE INACTIVE HOLE PER TRIAL                    *
\* X -NUMBER OF NOSE POKES IN THE ACTIVE HOLE PER SESSION
\* z -NUMBER OF NOSE POKES IN THE INACTIVE HOLE PER SESSION
\************************************************************************